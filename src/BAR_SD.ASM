;*****************************************
;
;     모드 13h용 게임 작성 라이브러리
;                       Ver 4.2
;
;                 제작자:
;                        주 경 민
;
;  게임 작성을 위해 만든 라이브러리로서
; 모드 13h (320x200, 256) 전용이다.
;
;*****************************************
IDEAL
include "game13h.inc"


UDATASEG


DATASEG


CODESEG

;******************************
;  그림자 진 사각형을 그린다.
; void bar_shade( int start_x, int start_y, int end_x, int end_y, char shade )
;******************************
proc bar_shade  start_x:word, start_y:word, end_x:word, end_y:word, shade:byte
  uses ds, es, si, di

  ; es:di = ds:si ... 그래픽 세그먼트, 오프셋
  set_page ax, [_page_number]
  mov ds, ax
  mov es, ax
  calc_addr si, [start_x], [start_y]
  mov di, si

  mov bx, [end_x]
  sub bx, [start_x]
  inc bx          ; bx ... 사각형의 x 크기
  mov cx, [end_y]
  sub cx, [start_y]
  inc cx          ; cx ... 사각형의 y 크기를 셔트

  mov dx, H_RES
  sub dx, bx      ; dx ... 320 - x_size
  mov ah, [shade] ; ah ... shade

  ; state :
  ;   es:di ... 화면상의 사각형 시작부분
  ;   bx ... x_size
  ;   cx ... y_size
  ;   dx ... carage (320 - x_size)

  cld

 @@y_loop:
  push cx
  mov cx, bx

 @@x_loop:
  lodsb
  add al, ah
  stosb
  loop @@x_loop

  add di, dx
  mov si, di
  pop cx
  loop @@y_loop

  ret
endp
global bar_shade : proc


END
