;*****************************************
;
;     모드 13h용 게임 작성 라이브러리
;                       Ver 4.2
;
;                 제작자:
;                        주 경 민
;
;  게임 작성을 위해 만든 라이브러리로서
; 모드 13h (320x200, 256) 전용이다.
;
;*****************************************
IDEAL
include "game13h.inc"


UDATASEG


DATASEG


CODESEG

;*****************
;  박스를 그린다.
; void box( int start_x, int start_y, int end_x, int end_y, byte color )
;*****************
proc box  start_x:word, start_y:word, end_x:word, end_y:word, color:byte
  uses es, di

  mov cx, [end_y]
  sub cx, [start_y]
  cmp cx, 2
  jge @@next
  call bar, [start_x], [start_y], [end_x], [end_y], [word color]
  jmp @@exit

 @@next:
  ; es:di ... 그래픽 세그먼트, 오프셋 세트
  ; not braken cx
  set_page es, [_page_number]
  calc_addr di, [start_x], [start_y]

  mov bx, [end_x]
  sub bx, [start_x]
  inc bx           ; bx ... 사각형의 x 크기
  dec cx           ; cx ... 사각형의 y 크기를 셔트 (2를 뺀 수)

  mov al, [color]
  mov ah, al    ; ah, al ... color

  mov dx, H_RES
  sub dx, bx    ; dx ... 320 - x_size

  ; state :
  ;   es:di ... 화면상의 사각형 시작부분
  ;   al,ah ... color
  ;   bx ... x_size
  ;   cx ... y_size
  ;   dx ... carage (320 - x_size)

  cld

  push cx
  mov cx, bx
  shr cx, 1
  jnc @@top_skip
  stosb
  jcxz @@no_put
 @@top_skip:
  rep stosw
 @@no_put:
  add di, dx
  pop cx
  jcxz @@bottom

  sub bx, 2
 @@loop:
  stosb
  add di, bx
  stosb
  add di, dx
  loop @@loop
  add bx, 2

 @@bottom:
  mov cx, bx
  shr cx, 1
  jnc @@bottom_skip
  stosb
  jcxz @@exit
 @@bottom_skip:
  rep stosw

 @@exit:
  ret
endp
global box : proc


END
