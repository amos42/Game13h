;*****************************************
;
;     모드 13h용 게임 작성 라이브러리
;                       Ver 4.2
;
;                 제작자:
;                        주 경 민
;
;  게임 작성을 위해 만든 라이브러리로서
; 모드 13h (320x200, 256) 전용이다.
;
;*****************************************
IDEAL
include "game13h.inc"


UDATASEG


DATASEG


CODESEG

;*******************************
;  현재의 팔레트를 정착시킨다.
; void fix_palette(void)
;*******************************
proc fix_palette
  uses es, di

  mov ax, DGROUP
  mov es, ax
  mov di, offset _palette

  xor al, al
  mov dx, 03C7h
  out dx, al
  mov cx, 768d
  mov dx, 03C9h

  cld
  rep insb

  ret
endp
global fix_palette : proc


;***************************************
;  현재의 팔려트를 정착시킨다.(배열로)
; void fix_palette_array( palette_256 *array, byte start_color, word change )
;***************************************
proc fix_palette_array  array:dword, start_color:byte, change:word
  uses es, di

  les di, [array]

  mov ax, [change]
  mov dx, 3
  mul dx
  mov cx, ax
  mov al, [start_color]
  mov dx, 03C7h
  out dx, al
  mov dx, 03C9h

  cld
  rep insb

  ret
endp
global fix_palette_array : proc


END
