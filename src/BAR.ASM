;*****************************************
;
;     모드 13h용 게임 작성 라이브러리
;                       Ver 4.2
;
;                 제작자:
;                        주 경 민
;
;  게임 작성을 위해 만든 라이브러리로서
; 모드 13h (320x200, 256) 전용이다.
;
;*****************************************
IDEAL
include "game13h.inc"


UDATASEG


DATASEG


CODESEG

;***************************
;  속이 찬 사각형을 그린다.
; void bar( int start_x, int start_y, int end_x, int end_y, byte color )
;***************************
proc bar  start_x:word, start_y:word, end_x:word, end_y:word, color:byte
  uses es, si, di

  ; es:di ... 그래픽 세그먼트, 오프셋
  set_page es, [_page_number]
  calc_addr di, [start_x], [start_y]

  mov bx, [end_x]
  sub bx, [start_x]
  inc bx                  ; bx ... 사각형의 x 크기
  mov cx, [end_y]
  sub cx, [start_y]
  inc cx                  ; cx ... 사각형의 y 크기를 셔트

  mov dx, H_RES
  sub dx, bx    ; dx ... 320 - x_size

  ; ah, al ... color
  mov al, [color]
  mov ah, al

  ; state :
  ;   es:di ... 화면상의 사각형 시작부분
  ;   ah,al ... color
  ;   bx ... x_size
  ;   cx ... y_size
  ;   dx ... carage (320 - x_size)

  cld

 @@loop:
  mov si, cx  ; push cx
  mov cx, bx

  shr cx, 1
  jnc @@skip
  stosb
 @@skip:
  jcxz @@no_put
  rep stosw
 @@no_put:

  add di, dx
  mov cx, si  ; pop cx
  loop @@loop

  ret
endp
global bar : proc


END
