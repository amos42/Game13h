;*****************************************
;
;     모드 13h용 게임 작성 라이브러리
;                       Ver 4.2
;
;                 제작자:
;                        주 경 민
;
;  게임 작성을 위해 만든 라이브러리로서
; 모드 13h (320x200, 256) 전용이다.
;
;*****************************************
IDEAL
include "game13h.inc"


UDATASEG


DATASEG


CODESEG

;********************************
;  죠이스틱의 위치를 입력받는다.
; void get_joy_stick( int *x, int *y )
;********************************
proc get_joy_stick  x:dword, y:dword
  uses es

  mov dx, 201h  ; 죠이스틱 포트 값

  mov cx, 1000
 @@ready_horiz:
  in al, dx
  test al, 00000001b
  loopnz @@ready_horiz
  out dx, al

  mov cx, 1000
 @@loop_horiz:
  in al, dx
  test al, 00000001b
  loopnz @@loop_horiz

  sub cx, 1000
  neg cx
  les bx, [x]
  mov [es:bx], cx

  mov cx, 1000
 @@ready_vert:
  in al, dx
  test al, 00000010b
  loopnz @@ready_vert
  out dx, al

  mov cx, 1000
 @@loop_vert:
  in al, dx
  test al, 00000010b
  loopnz @@loop_vert

  sub cx, 1000
  neg cx
  les bx, [y]
  mov [es:bx], cx

  ret
endp
global get_joy_stick : proc


;*********************************
;  죠이스틱의 버튼상태를 얻는다.
; int get_joy_button(void)
;*********************************
proc get_joy_button
  mov dx, 201h
  in al, dx

  not al
  and al, 00110000b
  shr al, 4
  xor ah, ah

  ret
endp
global get_joy_button : proc


END
