;*****************************************
;
;     모드 13h용 게임 작성 라이브러리
;                       Ver 4.2
;
;                 제작자:
;                        주 경 민
;
;  게임 작성을 위해 만든 라이브러리로서
; 모드 13h (320x200, 256) 전용이다.
;
;*****************************************
IDEAL
P286
MODEL Large, C
DOSSEG

  include "game13h.inc"


UDATASEG


DATASEG


CODESEG

;********************
;  수평선을 그린다.
; void line_horiz( int x, int y, word length, byte color )
;********************
proc line_horiz  x, y, length : word, color : byte
  uses es, di

  set_page es, [_page_number]
  calc_addr di, [x], [y]

  cld
  mov cx, [length]
  jcxz @@exit

  mov al, [color]
  mov ah, al
  shr cx, 1
  jnc @@work
  stosb
  jcxz @@exit
 @@work:
  rep stosw

 @@exit:
  ret
endp
global line_horiz : proc


;*********************
;  수직선을 그린다.
; void line_vert( int x, int y, word length, byte color )
;*********************
proc line_vert  x, y, length : word, color : byte
  uses es

  set_page es, [_page_number]
  calc_addr bx, [x], [y]

  mov cx, [length]
  jcxz @@exit

  mov al, [color]
 @@work:
  mov [es:bx], al
  add bx, H_RES
  loop @@work

 @@exit:
  ret
endp
global line_vert : proc


;********************
;  대각선을 그린다.
; void line_cross( int x, int y, word length, byte color )
;********************
proc line_cross  x, y, length : word, color : byte
  uses es

  set_page es, [_page_number]
  calc_addr bx, [x], [y]

  mov cx, [length]
  jcxz @@exit

  mov al, [color]
  mov ah, al
 @@work:
  mov [es:bx], al
  add bx, H_RES + 1
  loop @@work

 @@exit:
  ret
endp
global line_cross : proc


END
