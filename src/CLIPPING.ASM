;*****************************************
;
;     모드 13h용 게임 작성 라이브러리
;                       Ver 4.2
;
;                 제작자:
;                        주 경 민
;
;  게임 작성을 위해 만든 라이브러리로서
; 모드 13h (320x200, 256) 전용이다.
;
;*****************************************
IDEAL
include "game13h.inc"


UDATASEG


DATASEG


CODESEG

;***********************************
;         >> y의 클리핑 <<
;
; Input  : ax - 이미지의 y값
;          bx - 이미지의 x크기
;          cx - 이미지의 y크기
;          si - 이미지의 시작주소
; Ouptut : ax - 클리핑된 y값
;          cx - 클리핑된 y크기
;          si - 클리핑된 시작주소
; Braken : di, dx
;***********************************
proc _clipping_y
  uses ds

  mov dx, DGROUP
  mov ds, dx

  cmp ax, [_max_y]
  jg @@no_put

  mov di, [_min_y]
  cmp ax, di
  jge @@bottom_clip

  sub di, ax        ; 짤려 나가는 크기
  sub cx, di        ; y크기 클리핑
  mov ax, di
  mul bx            ; ax *= size_x
  add si, ax        ; 시작주소 클리핑
  mov ax, [_min_y]  ; y값 클리핑

 @@bottom_clip:
  mov dx, ax
  add dx, cx
  cmp dx, [_min_y]
  jl @@no_put

  mov di, [_max_y]
  sub dx, di
  jle @@exit
  sub cx, dx
  inc cx
  jmp @@exit

 @@no_put:
  xor cx, cx

 @@exit:
  ret
endp
global _clipping_y : proc


;***********************************
;         >> x의 클리핑 <<
;
; Input  : ax - 이미지의 x값
;          bx - 이미지의 x크기
;          si - 이미지의 시작주소
;          di - 비디오램의 시작주소
; Ouptut : bx - 클리핑된 x크기
;          si - 클리핑된 시작주소
;          di - 클리핑된 비디오램 주소
; Braken : ax, dx
;***********************************
proc _clipping_x
  uses ds

  mov dx, DGROUP
  mov ds, dx

  cmp ax, [_max_x]
  jg @@no_put

  mov dx, [_min_x]
  cmp ax, dx
  jge @@bottom_clip

  sub dx, ax        ; new_x - old_x
  add si, dx        ; 이미지 배열 증가
  sub bx, dx        ; x크기 클리핑
  add di, dx        ; 비디오 램 주소 클리핑
  mov ax, [_min_x]  ; x값 클리핑

 @@bottom_clip:
  add ax, bx
  cmp ax, [_min_x]
  jl @@no_put

  mov dx, [_max_x]
  sub ax, dx
  jle @@exit

  sub bx, ax
  inc bx
  jmp @@exit

 @@no_put:
  xor bx, bx

 @@exit:
  ret
endp
global _clipping_x : proc


END
