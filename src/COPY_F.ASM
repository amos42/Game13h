;*****************************************
;
;     모드 13h용 게임 작성 라이브러리
;                       Ver 4.2
;
;                 제작자:
;                        주 경 민
;
;  게임 작성을 위해 만든 라이브러리로서
; 모드 13h (320x200, 256) 전용이다.
;
;*****************************************
IDEAL
include "game13h.inc"


UDATASEG


DATASEG


CODESEG

;*******************************************
;  좌우로 반전해서 찍는다.(COPY_PUT)
; void copy_put_flip_hiriz( int x, int y, image_data image )
;*******************************************
proc copy_put_flip_horiz  x:word, y:word, image:dword
  uses ds, es, si, di

  ; es:di ... 그래픽 셔그먼트 세트
  set_page es, [_page_number]
  calc_addr di, [x], [y]

  ; ds:si ... 이미지 배열의 시작점
  lds si, [image]

  mov bx, [ds:si]    ; bx ... 이미지의 x 크기
  test bx, bx
  jz @@exit
  mov cx, [ds:si+2]  ; cx ... 이미지의 y 크기를 세트
  jcxz @@exit
  add si, 4

  mov dx, H_RES
  add dx, bx      ; ax ... 320 + x_size

  dec di
  add di, bx   ; move point left end

  ; state :
  ;   ds:si ... 배열의 이미지 시작부분
  ;   es:di ... 화면상의 이미지 시작부분
  ;   bx ... x_size
  ;   cx ... y_size
  ;   dx ... carage (320 + x_size)

  cld

 @@y_loop:
  push cx
  mov cx, bx

 @@x_loop:
  lodsb
  mov [es:di], al
  dec di
  loop @@x_loop

  add di, dx
  pop cx
  loop @@y_loop

 @@exit:
  ret
endp
global copy_put_flip_horiz : proc


;*******************************************
;  상하로 반전해서 찍는다.(COPY_PUT)
; void copy_put_flip_vert( int x, int y, image_data image )
;*******************************************
proc copy_put_flip_vert  x:word, y:word, image:dword
  uses ds, es, si, di

  ; es:di ... 그래픽 셔그먼트 세트
  set_page es, [_page_number]

  ; ds:si ... 이미지 배열의 시작점
  lds si, [image]

  mov bx, [ds:si]    ; bx ... 이미지의 x 크기
  test bx, bx
  jz @@exit
  mov cx, [ds:si+2]  ; cx ... 이미지의 y 크기를 세트
  jcxz @@exit
  add si, 4

  mov ax, [y]
  add ax, cx
  dec ax
  calc_addr di, [x], ax

  mov dx, H_RES
  add dx, bx      ; dx ... 320 + x_size

  ; state :
  ;   ds:si ... 배열의 이미지 시작부분
  ;   es:di ... 화면상의 이미지 시작부분
  ;   bx ... x_size
  ;   cx ... y_size
  ;   dx ... carage (320 + x_size)

  cld

 @@loop:
  mov ax, cx  ; push cx
  mov cx, bx

  shr cx, 1
  jnc @@skip
  movsb
  jcxz @@next
 @@skip:
  rep movsw

 @@next:
  sub di, dx
  mov cx, ax  ; pop cx
  loop @@loop

 @@exit:
  ret
endp
global copy_put_flip_vert : proc


;*******************************************
;  상하좌우로 반전해서 찍는다.(COPY_PUT)
; void copy_put_flip_both( int x, int y, image_data image )
;*******************************************
proc copy_put_flip_both  x:word, y:word, image:dword
  uses ds, es, si, di

  ; es:di ... 그래픽 셔그먼트 세트
  set_page es, [_page_number]

  ; ds:si ... 이미지 배열의 시작점
  lds si, [image]

  mov bx, [ds:si]    ; bx ... 이미지의 x 크기
  test bx, bx
  jz @@exit
  mov cx, [ds:si+2]  ; cx ... 이미지의 y 크기를 세트
  jcxz @@exit
  add si, 4

  mov ax, [y]
  add ax, cx
  dec ax
  calc_addr di, [x], ax

  mov dx, H_RES
  sub dx, bx      ; ax ... 320 - x_size

  dec di
  add di, bx   ; move point left end

  ; state :
  ;   ds:si ... 배열의 이미지 시작부분
  ;   es:di ... 화면상의 이미지 시작부분
  ;   bx ... x_size
  ;   cx ... y_size
  ;   dx ... carage (320 - x_size)

  cld

 @@y_loop:
  push cx
  mov cx, bx

 @@x_loop:
  lodsb
  mov [es:di], al
  dec di
  loop @@x_loop

  sub di, dx
  pop cx
  loop @@y_loop

 @@exit:
  ret
endp
global copy_put_flip_both : proc


END
