;*****************************************
;
;     모드 13h용 게임 작성 라이브러리
;                       Ver 4.2
;
;                 제작자:
;                        주 경 민
;
;  게임 작성을 위해 만든 라이브러리로서
; 모드 13h (320x200, 256) 전용이다.
;
;*****************************************
IDEAL
include "game13h.inc"


UDATASEG


DATASEG


CODESEG

;*********************
;  키버퍼를 제거한다.
; void clear_key(void)
;*********************
proc clear_key
  uses es

  mov ax, 40h
  mov es, ax

  mov ax, [es:1Ah]
  mov [es:1Ch], ax
  ret
endp
global clear_key : proc


;********************************************
;  키버퍼에 키값을 넣는다.(키버퍼는 클리어)
; void put_key( int key )
;********************************************
proc put_key  key:word
  uses es

  mov ax, 40h
  mov es, ax

  mov ax, [key]
  mov [es:1Eh], ax

  mov [word es:1Ah], 1Eh
  mov [word es:1Ch], 20h

  ret
endp
global put_key : proc


;********************
;  키를 입력받는다.
; int get_key(void)
;********************
proc get_key
 @@loop:
  xor ah, ah
  int 16h
  mov bx, ax
  mov ah, 1
  int 16h
  jnz @@loop

  test bl, bl
  jz @@no_change
  xor bh, bh
 @@no_change:
  mov ax, bx

  ret
endp
global get_key : proc


;******************************
;  실시간으로 키를 입력받는다.
; int action_key(void)
;******************************
proc action_key
  mov ah, 1
  int 16h
  jz @@skip

  call get_key
  jmp @@exit

 @@skip:
  xor ax, ax

 @@exit:
  ret
endp
global action_key : proc


;**************************
;  키보드 입력을 기다린다.
; void delay_key(void)
;**************************
proc delay_key
  uses es

  mov ax, 40h
  mov es, ax

  mov ax, [es:1Ah]
  mov [es:1Ch], ax

  xor ah, ah
  int 16h

  ret
endp
global delay_key : proc


END
